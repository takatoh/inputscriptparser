script : _NL* statement+

statement : line continued*

line : command _WS_INLINE arglist _WS_INLINE? _NL+
     | command _WS_INLINE? _NL+

continued : _INDENT arglist _WS_INLINE? _NL+

command : CMDNAME

CMDNAME : UCASE_LETTER (UCASE_LETTER|DIGIT|"-")*

arglist : arg
        | arglist _WS_INLINE arg

arg : number
    | string
    | keyword
    | boolean

number : SIGNED_NUMBER

string : ESCAPED_STRING

keyword : KWORD

KWORD : UCASE_LETTER (UCASE_LETTER|DIGIT)*

boolean : true
        | false

true : "true"
     | "yes"
     | "on"

false : "false"
      | "no"
      | "off"

_WS_INLINE : WS_INLINE

_NL : NEWLINE

_INDENT : /(?<=\n) +/


%import common.UCASE_LETTER
%import common.DIGIT
%import common.SIGNED_NUMBER
%import common.ESCAPED_STRING
%import common.NEWLINE
%import common.WS_INLINE
%import common.CPP_COMMENT

COMMENT : WS_INLINE? CPP_COMMENT

%ignore COMMENT
